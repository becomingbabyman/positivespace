ps.controller "SearchsHeaderCtrl", ["$scope", "$http", "$location", "$q", ($scope, $http, $location, $q) ->
    $scope.lastQuery = null
    t = null

    $scope.showInput = (e) ->
        $scope.showSearch = true
        window.setTimeout ->
            $('#search_header_query').focus()
        , 50
    $scope.hideInput = (e) ->
        log 'wat'
        $scope.showSearch = false unless ($scope.query and $scope.query.length > 0) or $('#search_header_query').is(':focus')

    $scope.updateScroll = (e) ->
        if e.keyCode in [38, 40]
            # $(".nav ul.typeahead").scrollTo(".active", 200)
            $.scrollTo("ul.typeahead .active", 200)

    $scope.search = (query) ->
        defered = $q.defer()

        if t? then clearTimeout(t)
        t = setTimeout ->
            $scope.loading = true
            $http.get("/api/users?q=#{query}").then (response) ->
                $scope.loading = false
                $scope.lastQuery = query
                if response.data?.collection? and response.data.collection.length > 0
                    results = response.data.collection
                else
                    results = [{typeahead: "No results for #{query}"}, {typeahead: "Search spaces by name, prompt, and location"}]
                defered.resolve(results)
            , (error) ->
                $scope.loading = false
        , 300

        defered.promise

    # TODO: REFACTOR: This is a crappy way to select a search result. Find a better way.
    $scope.$watch 'query', (query) ->
        if _.isObject(query)
            $scope.query = null
            if query.slug?
                $location.path("/#{query.slug}")
                analytics.track "search success",
                    query: $scope.lastQuery
                    username: query.username
                    id: query.id
            else if $scope.app.loggedIn()
                $location.path("/#{$scope.app.currentUser.slug}")
                analytics.track "search not found",
                    query: $scope.lastQuery
                    loggedIn: true
            else
                $scope.app.flash('info', 'Looks like you need to log in first.')
                $location.path("/register")
                analytics.track "search not found",
                    query: $scope.lastQuery
                    loggedIn: false
    , true
]
